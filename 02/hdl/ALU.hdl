// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
        // process input flags.
        Mux16(a=x, b=false, sel=zx, out=x1);
        // negate X
        Not16(in=x1, out=notX);
        Mux16(a=x1, b=notX, sel=nx, out=x2);

        Mux16(a=y, b=false, sel=zy, out=y1);
        // negate Y
        Not16(in=y1, out=notY);
        Mux16(a=y1, b=notY, sel=ny, out=y2);

        // perform the operation
        Add16(a=x2, b=y2, out=xplusy);
        And16(a=x2, b=y2, out=xandy);

        // select
        // f = 0 => x & y
        // f = 1 => x + y
        Mux16(a=xandy, b=xplusy, sel=f, out=tempOut);

        // negate output and select if needed
        Not16(in=tempOut, out=notTempOut);
        Mux16(a=tempOut, b=notTempOut, sel=no,
            out=out,
            // split output so that we can use or8way on it
            out[0..7]=out1,
            out[8..15]=out2,

            // set negative flag.
            // a number is negative (2's complement) IFF msb is set
            out[15]=ng);

        // set zero flag
        Or8Way(in=out1, out=zer0);
        Or8Way(in=out2, out=zer1);
        Or(a=zer0, b=zer1, out=negZr);
        Not(in=negZr, out=zr);
}
